---
title: "Zen of Nim"
author: "Andreas Rumpf (Araq)"
excerpt: "transcript of Zen of Nim presentation at NimConf2021"
---

<div class="sidebarblock">
  <div class="content">
    <div class="paragraph">
      This is a transcript of Araq's presentation at NimConf2021 delivered on June 26th
      (see the video on <a href="https://www.youtube.com/watch?v=D_G9h7DcIqM&t=240s">youtube</a>,
      check the slides' source on <a href="https://github.com/Araq/nimconf2021">github</a>).
      It has been adapted to blog post format by <a href="https://github.com/pietroppeter">Pietro Peterlongo</a>.
    </div>
  </div>
</div>
<!-- github repo should have pages active and slide linked from readme, will open a PR for that-->

# Zen of Nim

<!--it makes sense to have the final set of rules explicit as first thing, should add an editor's note here-->

1. Copying bad design is not good design.
2. If the compiler cannot reason about the code, neither can the programmer.
3. Don't get in the programmer's way.
4. Move work to compile-time: Programs are run more often than they are compiled.
5. Customizable memory management.
6. Concise code is not in conflict with readability, it enables readability.
7. (Leverage meta programming to keep the language small.)
8. Optimization is specialization: When you need more speed, write custom code.
9. There should be one and only one programming language for everything. That language is Nim.

<!--
Overview of structure:
- syntax section provides context for rule 6: concise code enables readability
- "compiler" section is about rule 2: compilers must be able to reason about code
- "meta programming" section is introduced with rule 1 (copying bad design ...)
- a "pragmatic" section explains rule 3 (don't get in programmer's way)
- last section is on rule 5 (customizable memory management)
- rule 4, 7, 8 and 9 are only mentioned in the final summary of zen of nim
-->

# Introduction

In this blog post I will explain the philosophy of Nim language and why Nim can be useful for a wide range of application domains, such as:

- scientific computing
- games
- compilers
- operating systems development
- scripting
- everything else

<!-- could be nice to add links to other nim conf talks related to these domains-->

"Zen" means that we will arrive at a set of rules (shown above) that guide the language design and evolution,
but I will go through these rules via examples.

# Syntax

Let me introduce Nim via its syntax.
I am aware that most of you probably know the language, but to give you a gentle introduction even if you have not seen it before,
I will explain basic syntax and also come to interesting conclusions.

Nim uses an **indentation based syntax** as inspired by Haskell or Python that **fits Nim's macro system**.


## Function application

Nim distinguish between statements and expressions and most of the time an expression is function application (or procedure call).
Function application uses the traditional mathy syntax with the parentheses: `f()`, `f(a)`, `f(a, b)`.

And here is the sugar:

|   |  Sugar     |   Meaning           |   Example                      |
|---|------------|---------------------|--------------------------------|
| 1 |  `f a`     |   `f(a)`            |   `spawn log("some message")`  |
| 2 |  `f a, b`  |   `f(a, b)`         |   `echo "hello ", "world"`     |
| 3 |  `a.f()`   |   `f(a)`            |   `db.fetchRow()`              |
| 4 |  `a.f`     |   `f(a)`            |   `mystring.len`               |
| 5 |  `a.f(b)`  |   `f(a, b)`         |   `myarray.map(f)`             |
| 6 |  `a.f b`   |   `f(a, b)`         |   `db.fetchRow 1`              |
| 7 |  `f"\n"`   |   `f(r"\n")`        |   `re"\b[a-z*]\b"`             |
| 8 |  `f a: b`  |   `f(a, b)`         |   `lock x: echo "hi"`          |


* in rules 1 and 2 you can leave out the parentheses and there are examples so that you can see why that might be useful: `spawn` looks like a keyword, which is good, since it does something special; `echo` is also famous for leaving out the parentheses because usually you write these statements for debugging, thus you are in a hurry to get things done;
* you have dot notations available and you can leave out parentheses (rules 3-6);
* rule 7 is about string literals: `f` followed by a string without whitespace is still a call but the  string is turned into a raw string literal,
  which is very handy for regular expressions because regular expressions have their own idea of what a backslash is supposed to mean;
* finally in the last rule we can see that you can pass a block a block of code to the `f` with a `:` syntax and the cide block is usually the last argument you pass to the function and this can be used to create custom `lock` statement;
* there is one **exception** to leaving out the parentheses, in the case you want to refer to `f` directly: `f` does not mean `f()`. In the case of `myarray.map(f)` you do not want to invoke `f`, instead you want to pass the `f` itself to `map`.

## Operators

Nim has binary and unary operators:

* Most of the time binary operators are simply invoked as `x @ y`
  and unary operators as `@x`.
* No explicit distinction between operators and functions and between binary and unary operators.

```nim
func `++`(x: var int; y: int = 1; z: int = 0) =
  x = x + y + z

var g = 70
++g
g ++ 7
# operator in backticks is treated like an 'f':
g.`++`(10, 20)
echo g  # writes 108
```

* operators are simply sugar for functions
* the operator token goes inside backticks (e.g. `++`) when the function is defined and it can be called as a function using backticks notation.
  
Recall that `var` keyword indicates mutability:
* parameters are readonly unless declared as `var`
* `var` means "pass by reference" (implemented with a hidden pointer)


## Statements vs expressions

Statements require indentation:

```nim
# no indentation needed for single assignment statement:
if x: x = false

# indentation needed for nested if statement:
if x:
  if y:
    y = false
else:
  y = true

# indentation needed, because two statements
# follow the condition:
if x:
  x = false
  y = false
```

You can also use semicolons instead of new lines but this is very uncommon in Nim.

<!-- should we add an example for semicolons? something like the python-walrus-equivalent syntax as in: https://forum.nim-lang.org/t/4637#28963 -->

Expressions are not really based on indentation so you are free to use additional white space within expressions:

```nim
if thisIsaLongCondition() and
    thisIsAnotherLongCondition(1,
        2, 3, 4):
  x = true
```

This can be very handy for breaking up long lines, as a rule of thumb you have optional indentation after operators, `(` and `,`.

Finally `if`, `case`, etc statements are also available as expressions, so they can produce a value.

As a simple example to conclude this section,
here is a full nim program to show a little bit more of syntax.
If you are familiar with Python, this should be straightforward to read:

```nim
func indexOf(s: string; x: set[char]): int =
  for i in 0..<s.len:
    if s[i] in x: return i
  return -1

let whitespacePos = indexOf("abc def", {' ', '\t'})
echo whitespacePos
```

* Nim uses static typing, so the parameters have types attached: the input parameter named `s` has type `string`;
  `x` has type set of characters; the function called `indexOf` produces an integer value as final result.
* you can iterate over the string index via the `for` loop, the goal is to find the position of the first character
  inside the string that matches the given set of values.
* when calling the function, we construct a set of whitespaces using `{}`.

Our first rule in our Zen of Nim discussion is:

<div class="zen-rule"><!-- visualized as bigger font inside a centered rounded box? -->
Concise code is not in conflict with readability, it enables readability.
</div>

As you can see in the above tiny example, it is very easy to follow and to read because we basically leave out the symbols
that carry little meaning, such as curly braces for blocks or semicolons to terminate the statements.
This scales up, so in longer programs it is really helpful when you have less code to look at because then you can
more easily figure out how the code is supposed to work or what it can do without getting too much details.

Usually the argument is like: "the syntax is terse, so it is unreadable and all you want to do is to save typing work";
in my opinion this totally misses the point, it is not about saving keystrokes or saving typing effort,
it is saving the effort when you look at the resulting code.
Programs are read way more often than they are written and when you read them it really helps if they are shorter.

## A smart compiler

A second rule for our Zen of Nim would be:

<div class="zen-rule">
The compiler must be able to reason about the code
</div>

This means we want:

- Structured programming.
- Static typing!
- Static binding!
- Side effects tracking.
- Exception tracking.
- Mutability restrictions (the enemy is shared mutable state, but if the stats is not shared it is fine to mutate so we want to be able to do it precisely).
- Value based datatypes. (Aliasing is very hard to reason about!)

We will see now what these points mean in detail.

### Structured programming

In the following example the task is to count all the words in the file (given by `filename` as a `string`),
and produce a count table of strings, so in the end there will be entries for every word on how often it occurs in the text.

```nim
import tables, strutils

proc countWords(filename: string): CountTable[string] =
  ## Counts all the words in the file.
  result = initCountTable[string]()
  for word in readFile(filename).split:
    result.inc word
  # 'result' instead of 'return', no unstructed control flow
```

Thankfully, the Nim standard library already offers us a `CountTable` so the first line of the `proc`
is the new count table.
`result` is built into Nim and represent the return value so you
do not have to write `return result` which is unstructured programming because `return` immediately
leaves every scope and returns back the result.
Nim does offer the `return` statement but we advise you to avoid it for this reason, since that is unstructured programming.

In the rest of `proc`, we read the file into a single buffer, we split to the get the single word and we count the word via
`result.inc`.

Once again, structured programming means you have a single entry point into a block of code and a single leaving point in the code.

In the next example, with `continue` statement I leave the `for` loop body in a more convoluted manner:

```nim
for item in collection:
  if item.isBad: continue
  # what do we know here at this point?
  use item
```

* for every item in this collection if the item is bad we continue and otherwise after we use the item.
* what do I know after the continue statement? well, I know that the item is not bad

Why then not write it like in this way where we use structured programming:

```nim
for item in collection:
  if not item.isBad:
    # what do we know here at this point?
    # that the item is not bad.
    use item
```

* indentation here gives us clues about the invariance in our code, so that we can see much more easily
  that when I `use item` the invariant holds that the item is not bad.

If you prefer the `continue` and `return` statement it is fine, it is not a crime to use them,
I use them myself if nothing else works, but you should avoid it and more importantly it means that
we will probably never add a more general go-to statement to the Nim programming language
because it is even more against the structured programming paradigm.
We want to be in this position where we can prove more and more properties about your code
and structured programming programming makes much easier for a proof engine to help with this.

### Static typing

Another example for static typing is that we really want you to use custom types
dedicated to the problem domain.

Here we have a little example showing you the `distinct string` feature:

```nim
type
  SandboxFlag = enum         ## what the interpreter should allow
    allowCast,               ## allow unsafe language feature: 'cast'
    allowFFI,                ## allow the FFI
    allowInfiniteLoops       ## allow endless loops

  NimCode = distinct string

proc runNimCode(code: NimCode; flags: set[SandboxFlag] = {allowCast, allowFFI}) =
  ...
```

* `NimCode` actually can be stored in a `string` but it is a `distinct string` so it is a special type
  with special rules
* the `proc runNimCode` can run arbitrary Nim code that you passed to it, but it is like a virtual machine
  that can run the code and it can restrict what is possible
* there is a sandbox environment in this example and there are custom properties that you might want to
  use so for instance you can say: allow the nim `cast` operation (`allowCast`) or allow the function foreign interface (`allowFFI`);
  the last option possible would be to allow Nim code to run into infinite loops (`allowInfiniteLoops`)
* we put the options in an ordinary `enum` and than we can produce a `set` of enums, indicating that
  every option is independent of the others

If you compare the above to C for instance, where it is common to use the same mechanism, you lose the type safety:

```c
#define allowCast (1 << 0)
#define allowFFI (1 << 1)
#define allowInfiniteLoops (1 << 2)

void runNimCode(char* code, unsigned int flags = allowCast|allowFFI);

runNimCode("4+5", 700); // nobody stops us from passing 700
```

* when calling `runNimCode`, ``flags` is only an unsigned integer and nobody stops from passing the value 700
even though it does not make any sense
* you need to use bit twiddling operations to define `allowCast`, ... `allowInfiniteLoops`

You lose information here: even though it is very much in the programmer's head what is really a valid
value for this `flags` argument, yet it is not written down in the program, so the compiler cannot really help you.

### Static binding

We want Nim to use static binding. Here is a modified "hello world" example:

```nim
echo "hello ", "world", 99
```

what happens is that the compiler rewrites the statment to:

```nim
echo([$"hello ", $"world", $99])
```

- echo is declared as: ``proc echo(a: varargs[string, `$`]);``
- `$` (Nim's `toString` operator) is applied to every argument
- overloading (of `$` operator in this case) instead of dynamic binding (as it would be done for example in C#)

This approach is extensible:

```nim
proc `$`(x: MyObject): string = x.s
var obj = MyObject(s: "xyz")
echo obj  # works
```

### Value based datatypes

We want value based data types so that the program becomes easier to reason about.
I have already said we want to restrict the shared mutable state.
One solution that is usually overlooked by functional programming languages is that in order to do that
you want to restrict aliasing and not the mutation.
The mutation is very direct, convenient and efficient.

```nim
type
  Rect = object
    x, y, w, h: int

# construction:
let r = Rect(x: 12, y: 22, w: 40, h: 80)

# field access:
echo r.x, " ", r.y

# assignment does copy:
var other = r
other.x = 10
assert r.x == 12
```

The fact that the assignment `other = r` performed a copy, means that there is no spooky action at a distance involved here,
there is only one access path to `r.x` and `other.x` is not an access path to the same memory location.

### Side effects tracking

We want to be able to track side effects.
Here is an example where the goal is to count the number of substrings inside a string.

```nim
import strutils

proc count(s: string, sub: string): int {.noSideEffect.} =
  result = 0
  var i = 0
  while true:
    i = s.find(sub, i)
    if i < 0: break
    echo "i is: ", i  # error: 'echo' can have side effects
    i += sub.len
    inc result
```

Let us assume that this is not correct code and there is a debug `echo` statement.
The compiler would complain: you say proc has no side effect but echo produces a side effect,
so you are wrong, go fix your code!

The other aspect of Nim is that while the compiler is very smart and can help you, sometimes you need to get
work done and you must be able to override these very good defaults.

So if I say: "hey, okay, I know this just produces a side effect, but I don't care
because this is only code I added for debugging" you can say: "hey, cast this body of code
to a `noSideEffect` effect" and then the compiler is happy and says "ok, go ahead":

```nim
import strutils

proc count(s: string, sub: string): int {.noSideEffect.} =
  result = 0
  var i = 0
  while true:
    i = s.find(sub, i)
    if i < 0: break
    {.cast(noSideEffect).}:
      echo "i is: ", i  # 'cast', so go ahead
    i += sub.len
    inc result
```

`cast` means, I know what I am doing, leave me alone.

### Exception tracking

We want exception tracking!

Here I have my main `proc` and I want to say it raises nothing,
I want to be able to ensure that I handled every exception that can happen:

```nim
import os

proc main() {.raises: [].} =
  copyDir("from", "to")
  # Error: copyDir("from", "to") can raise an
  # unlisted exception: ref OSError
```

the compiler would complain and say
"look, this is wrong, `copyDir` can raise an unlisted exception,
namely `OSError`".
So you say, "fine, in fact I did not handle it", but now I can claim
actually that `main` raises `OSError` and the compilers says: "you are right!":

```nim
import os

proc main() {.raises: [OSError].} =
  copyDir("from", "to")
  # compiles :-)
```

We want to be able to parametrize over this a little bit:

```nim
proc x[E]() {.raises: [E].} =
  raise newException(E, "text here")

try:
  x[ValueError]()
except ValueError:
  echo "good"
```

- I have a generic `proc x[E]` (`E` is the generic type), and I say: "whatever `E` you pass to this `x`,
  that is what I am going to raise
- Then I instantiate this `x` with this `ValueError` exception and the compiler is happy!

I was really surprised that this works out of the box.
When I came up with this example I was quite sure the compiler would produce a bug, but it is already
handling this situation very well and I think the reason for that is that somebody else helped out and fixed this bug.

### Mutability restrictions

Here I am going to show and explain what this experimental `strictFuncs` call does:

```nim
{.experimental: "strictFuncs".}

type
  Node = ref object
    next, prev: Node
    data: string

func len(n: Node): int =
  var it = n
  result = 0
  while it != nil:
    inc result
    it = it.next
```

- I have a `Node` type which is a `ref object` and `next` and `prev` are pointers to these kind of objects
  (it is a doubly linked list). There is also a `data` field of type `string`.
- I have a function `len` and it counts the number of nodes that are in my linked list.
- the implementation is pretty straightforward: unless it is `nil` you count the node and then follow to `next` node.

The crucial point is that via `strictFuncs` we tell the compiler that parameters are now **deeply** immutable,
so the compiler is fine with this code and it is also fine with these example:

```nim
{.experimental: "strictFuncs".}

func insert(x: var seq[Node]; y: Node) =
  let L = x.len
  x.setLen L + 1
  x[L] = y
```

I want to `insert` something but it is a `func` so it is **very** strict about my mutabilities.
Say I want to append to `x` which is a sequence of nodes, so `x` is **explicitly** mutable
via this `var` keyword (and `y` is not mutable).
So I can set `x` length as the old length plus one and then overwrite what is in there, and this is fine.

Finally, I can still mutate local state:

```nim
func doesCompile(n: Node) =
  var m = Node()
  m.data = "abc"
```

I have a variable of type `Node`, but it is freshly created and then I mutate it and set the `data` field and
since it is not connected to `n` the compiler is happy.

The semantics are: "you cannot mutate what is reachable via parameters unless these parameters are explicitly marked as `var`".

Here is an example where the compiler says:
"yeah, look, no, you are trying to mutate `n`, but you are in `strictFunc` mode so you are not allowed to do that"

```nim
{.experimental: "strictFuncs".}

func doesNotCompile(n: Node) =
  n.data = "abc"
```

We can now play these games and see how smart the compiler is.

Here I try to trick into accepting the code but I could not:

```nim
{.experimental: "strictFuncs".}

func select(a, b: Node): Node = b

func mutate(n: Node) =
  var it = n
  let x = it
  let y = x
  let z = y # <-- is the statement that connected
            # the mutation to the parameter

  select(x, z).data = "tricky" # <-- the mutation is here
  # Error: an object reachable from 'n'
  # is potentially mutated
```

- `select` is a helper function that takes two nodes and simply returns the second one
- then I want to mutate `n` but I assign it to `it`, and then `it` to `x`, `x` to `y`, and `y` to `z`
- then I select either `x` or `z` and then mutate the `data` field and overwrite the string to value `"tricky"`

The compiler will tell you "Error, an object reachable from `n` is potentially mutated"
and it will point out the statement that connects the graph to this parameter.
What it does internally is: it has a notion of an abstract graph and it starts with
"hey, every graph that is constructed is disjoined", but depending on the body of your function,
these disjoint graphs can be connected.
Then when you mutate something and the graph is mutated and it is connected to an input parameter,
the compiler will complain. So the second rule is:

<div class="zen-rule">
If the compiler cannot reason about the code,
neither can the programmer.
</div>

We really want a smart compiler helping you out, because programming is quite hard.

## Meta programming features

Another rule that is kind of famous by now is:

<div class="zen-rule">
Copying bad design is not good design
</div>

Bad: Language X has feature F, let's have that too!

("C++ has compile-time function evaluation, let's have that too!")

Good: We have many use cases for feature F.

<!-- in this case F is macro system -->

> ("We need to be able to do locking, logging, lazy evaluation,
> a typesafe Writeln/Printf, a declarative UI description language,
> async and parallel programming! So instead of building these
> features into the language, let's have a macro system.")



Templates for lazy evaluation:

```nim
template log(msg: string) =
  if debug:
    echo msg

log("x: " & $x & ", y: " & $y)
```

Roughly comparable to:

```c
#define log(msg) \
  if (debug) { \
    print(msg); \
  }

log("x: " + x.toString() + ", y: " + y.toString());
```

<!-- usual would be to evaluate an expression and call the function -->

Templates for control flow abstraction:

<!-- in C# it is a language primitive, in nim you implement it with a template -->

```nim
template withLock(lock, body) =
  var lock: Lock
  try:
    acquire lock
    body
  finally:
    release lock

withLock myLock:
  accessProtectedResource()
```

Macros to implement DSLs:

```nim
html mainPage:
  head:
    title "Zen of Nim"
  body:
    ul:
      li "A bunch of rules that make no sense."

echo mainPage()
```

Produces::

```html
<html>
  <head><title>Zen of Nim</title></head>
  <body>
    <ul>
      <li>A bunch of rules that make no sense.</li>
    </ul>
  </body>
</html>
```


#### Lifting

```nim
import math

template liftFromScalar(fname) =
  proc fname[T](x: openArray[T]): seq[T] =
    result = newSeq[typeof(x[0])](x.len)
    for i in 0..<x.len:
      result[i] = fname(x[i])

# make sqrt() work for sequences:
liftFromScalar(sqrt)
echo sqrt(@[4.0, 16.0, 25.0, 36.0])
# => @[2.0, 4.0, 5.0, 6.0]
```


#### Declarative programming

<!-- you can use templates to turn imperative into declarative programming -->

```nim
proc threadTests(r: var Results, cat: Category,
                  options: string) =
  template test(filename: untyped) =
    testSpec r, makeTest("tests/threads" / filename,
      options, cat, actionRun)
    testSpec r, makeTest("tests/threads" / filename,
      options & " -d:release", cat, actionRun)
    testSpec r, makeTest("tests/threads" / filename,
      options & " --tlsEmulation:on", cat, actionRun)

  test "tactors"
  test "tactors2"
  test "threadex"
```

####Â Varargs

```nim
test "tactors"
test "tactors2"
test "threadex"
```

```nim
test "tactors", "tactors2", "threadex"
```

```nim
import macros

macro apply(caller: untyped;
            args: varargs[untyped]): untyped =
  result = newStmtList()
  for a in args:
    result.add(newCall(caller, a))

apply test, "tactors", "tactors2", "threadex"
```

<!-- details are not important and ... -->

#### Typesafe Writeln/Printf

```nim
proc write(f: File; a: int) = echo a
proc write(f: File; a: bool) = echo a
proc write(f: File; a: float) = echo a

proc writeNewline(f: File) =
  echo "\n"

macro writeln*(f: File; args: varargs[typed]) =
  result = newStmtList()
  for a in args:
    result.add newCall(bindSym"write", f, a)
  result.add newCall(bindSym"writeNewline", f)
```

## A pragmatic approach

Don't get in the programmer's way

<!-- yes, the compiler is smart but don't get in the programmer's way -->

- Interoperability with C++, C and JavaScript.
- By compiling Nim to these languages.
- Low level features are available:

  * Bit twiddling
  * unsafe type conversions ("cast")
  * raw pointers.

<!--
the philosophy is not let's use C++ and Nim because Nim does have a particular feature
interfacing with C++ is last resort but then real world kicks in

ideally you only use nim language
-->

### Emit pragma

<!-- the interoperability story goes so far that we  -->

```nim
{.emit: """
static int cvariable = 420;
""".}

proc embedsC() =
  var nimVar = 89
  {.emit: ["""fprintf(stdout, "%d\n", cvariable + (int)""",
    nimVar, ");"].}

embedsC()
```

<!--
we want you to be able to get things done
we want the language to be practical
-->


<!-- transtion to talk about memory management, still under don't get in programmer way? no!-->

## Customizable memory management

### Sink parameters

<!-- destructor and ownerships are hopefully familiar from C++ and Rust -->

```nim
func f(x: sink string) =
  discard "do nothing"

f "abc"
```

Compile with `nim c --gc:orc --expandArc:f $file`

<!-- did I produce a memory leak? show me ... -->

```nim
func f(x: sink string) =
  discard "do nothing"
  `=destroy`(x)
```

**Nim's intermediate language is Nim itself.**

a different example:

```nim
var g: string

proc f(x: sink string) =
  g = x

f "abc"
```

we ask the compiler what does it do:

```nim
var g: string

proc f(x: sink string) =
  `=sink`(g, x)

f "abc"
```

<!-- x was moved into g and then it was destructed but compiler optimized -->

```nim
var g: string

proc f(x: sink string) =
  `=sink`(g, x)
  # optimized out:
  wasMoved(x)
  `=destroy`(x)

f "abc"
```

Destructors

```nim
type
  myseq*[T] = object
    len, cap: int
    data: ptr UncheckedArray[T]

proc `=destroy`*[T](x: var myseq[T]) =
  if x.data != nil:
    for i in 0..<x.len: `=destroy`(x[i])
    dealloc(x.data)
```

Move operator

```nim
proc `=sink`*[T](a: var myseq[T]; b: myseq[T]) =
  # move assignment, optional.
  # Compiler is using `=destroy` and
  # `copyMem` when not provided
  `=destroy`(a)
  a.len = b.len
  a.cap = b.cap
  a.data = b.data
```

Assignment operator

```nim
proc `=copy`*[T](a: var myseq[T]; b: myseq[T]) =
  # do nothing for self-assignments:
  if a.data == b.data: return
  `=destroy`(a)
  a.len = b.len
  a.cap = b.cap
  if b.data != nil:
    a.data = cast[typeof(a.data)](
      alloc(a.cap * sizeof(T)))
    for i in 0..<a.len:
      a.data[i] = b.data[i]
```

<!-- this accessors part is not discussed during presentation -->
Accessors

```nim
proc add*[T](x: var myseq[T]; y: sink T) =
  if x.len >= x.cap: resize(x)
  x.data[x.len] = y
  inc x.len

proc `[]`*[T](x: myseq[T]; i: Natural): lent T =
  assert i < x.len
  x.data[i]

proc `[]=`*[T](x: var myseq[T]; i: Natural; y: sink T) =
  assert i < x.len
  x.data[i] = y
```

Customizable memory management

# Zen of Nim

- Copying bad design is not good design.
- If the compiler cannot reason about the code, neither can the programmer.
- Don't get in the programmer's way.
- Move work to compile-time: Programs are run more often than they are compiled.
- Customizable memory management.
- Concise code is not in conflict with readability, it enables readability.
- (Leverage meta programming to keep the language small.)
- Optimization is specialization: When you need more speed, write custom code.
- There should be one and only one programming language for everything. That language is Nim.

<!-- there is friction between we want the language to be complete vs we want

optimization is specialization: was not covered before. when you need more speed.
-->

<!--how to add custom styling for a jekyll blog page? does the following work? need to install jekyll to try it-->
<style>
  .zen-rule {
  border-style: solid;
  border-radius: 5px;
  text-align: center;
  font-size: large;
  padding: 10px;
  }
</style>